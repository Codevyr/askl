COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
WHITESPACE = _{(" " | "\t" | NEWLINE)+}
ident = @{ XID_START ~ XID_CONTINUE* }
string = @{(!"\"" ~ ANY)*}
quoted_string = _{ "\"" ~ string  ~ "\""}
named_argument = ${ident ~ "=" ~ quoted_string}
generic_verb = ${ "@" ~ ident ~ ("(" ~ named_argument ~ ("," ~ named_argument)* ~ ")")?}
plain_filter = { quoted_string }
forced_verb = { "!" ~ quoted_string }
special_verb = _{ plain_filter | forced_verb }
verb = { generic_verb | special_verb}
scope = { "{" ~ statements ~ "}" }
statement = {(verb* ~ scope) | (verb+) | (scope)}
statement_terminator = _{";"}
statements = _{ (statement_terminator* ~ statement)* }
ask = _{SOI ~ statements ~ EOI }